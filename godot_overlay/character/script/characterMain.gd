# ìºë¦­í„° ê°ì²´ ìŠ¤í¬ë¦½íŠ¸ â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
# ì±„íŒ…ì´ ì…ë ¥ë˜ë©´ í™”ë©´ì— ìƒì„±ë˜ëŠ” ìºë¦­í„°ì˜ ë™ì‘, ìƒíƒœ ì „í™˜, ë¬¼ë¦¬ ì²˜ë¦¬ë¥¼ ë‹´ë‹¹í•˜ëŠ” ë©”ì¸ ìŠ¤í¬ë¦½íŠ¸ì…ë‹ˆë‹¤.
# ì‚¬ìš©ìì˜ ì±„íŒ…ì— ë°˜ì‘í•˜ì—¬ ë§í’ì„ ì„ í‘œì‹œí•˜ê³ , ëª…ë ¹ì–´ ì¼ë¶€ë¥¼ ì²˜ë¦¬í•˜ë©°, ë‹¤ì–‘í•œ ë¬¼ë¦¬ ê¸°ë°˜ ë™ì‘ì„ ìˆ˜í–‰í•©ë‹ˆë‹¤.
# â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ #

extends CharacterBody2D

# ìºë¦­í„°ì˜ ìƒíƒœë¥¼ ì •ì˜í•˜ëŠ” enumì…ë‹ˆë‹¤. (State Machine êµ¬ì¡°)
enum State { IDLE, WALK, JUMP, FALL, DANCE, SIT, GRAB, THROWN }
@export var currentState: State = State.WALK

# ë…¸ë“œ ì°¸ì¡° ë° ì´ˆê¸° ì„¤ì •
@onready var nameTag = $Nametag
@onready var sprite = $sprite
@onready var chatBubble: PackedScene = preload("res://character/chatBubble.tscn")
@onready var userDatabase = get_node("/root/mainWindow/UserDatabase")
@onready var defaultSpriteSet = preload("res://character/default.res")

var userData: chatUser

# ì´ë™ ë° ìƒíƒœ ê´€ë ¨ ë³€ìˆ˜
var direction = 0							# ìºë¦­í„°ì˜ ì´ë™ ë°©í–¥ (-1: ì™¼ìª½, 1: ì˜¤ë¥¸ìª½)
var stateTimer = 0							# í˜„ì¬ ìƒíƒœ ì§€ì† ì‹œê°„
var turnChance = 0.001						# ëœë¤í•˜ê²Œ ë°©í–¥ì„ ë°”ê¿€ í™•ë¥ 
var nextStateChange = 0						# ë‹¤ìŒ ìƒíƒœ ë³€ê²½ê¹Œì§€ì˜ ì‹œê°„

# ë“œë˜ê·¸ ì•¤ ë“œë¡­ ê´€ë ¨ ë³€ìˆ˜
var isDragged: bool = false					# í˜„ì¬ ë“œë˜ê·¸ ì¤‘ì¸ì§€ ì—¬ë¶€
var animationOverride: bool = false			# ì• ë‹ˆë©”ì´ì…˜ ì˜¤ë²„ë¼ì´ë“œ ìƒíƒœ (íŠ¹ì • ì• ë‹ˆë©”ì´ì…˜ ê°•ì œ ì¬ìƒ ì‹œ ì‚¬ìš©)
var dragOffset: Vector2 = Vector2.ZERO		# ë“œë˜ê·¸ ì‹œ ë§ˆìš°ìŠ¤ì™€ ìºë¦­í„° ìœ„ì¹˜ì˜ ì˜¤í”„ì…‹
var lastPosition: Vector2 = Vector2.ZERO 	# ì´ì „ í”„ë ˆì„ì˜ ìœ„ì¹˜ (ë˜ì§€ê¸° ì†ë„ ê³„ì‚°ìš©)
var throwVelocity: Vector2 = Vector2.ZERO 	# ë˜ì ¸ì¡Œì„ ë•Œì˜ ì†ë„
var mouseOver: bool = false					# ë§ˆìš°ìŠ¤ê°€ ìºë¦­í„° ìœ„ì— ìˆëŠ”ì§€ ì—¬ë¶€

var viewportRect: Rect2						# í˜„ì¬ ë·°í¬íŠ¸ì˜ í¬ê¸°
var boundaryMargin: float = 50.0			# í™”ë©´ ë°– íŒì •ì„ ìœ„í•œ ì—¬ìœ  ê³µê°„
var respawnCallback: Callable				# í™”ë©´ ë°–ìœ¼ë¡œ ë‚˜ê°”ì„ ë•Œ í˜¸ì¶œí•  ì½œë°± í•¨ìˆ˜

# ìºë¦­í„° ìƒ‰ìƒ íŒ”ë ˆíŠ¸ - ê° ìƒ‰ìƒ ì´ë¦„ê³¼ Hue Shift ê°’ì„ ì •ì˜í•©ë‹ˆë‹¤.
# í•´ë‹¹ íŒ”ë ˆíŠ¸ ê°’ì€ chatUser.gdì™€ ë™ì¼í•˜ê²Œ ì •ì˜ë˜ì–´ì•¼ í•©ë‹ˆë‹¤.
const COLOR_PALETTE = {
	"ì˜¤ë¦¬ì§€ë„": 0.0,
	"ë¸”ë£¨ë² ë¦¬": 0.05,
	"í¬ë„": 0.2,
	"ë³µìˆ­ì•„": 0.3,
	"ë”¸ê¸°": 0.45,
	"ì˜¤ë Œì§€": 0.55,
	"ë ˆëª¬": 0.6,
	"ë¼ì„":0.7,
	"ë…¹ì°¨": 0.8,
	"ë¯¼íŠ¸": 0.92
}

func _ready() -> void:
	direction = [-1, 1].pick_random()		# ì´ˆê¸° ì´ë™ ë°©í–¥ì„ ëœë¤í•˜ê²Œ ì„¤ì •í•©ë‹ˆë‹¤.
	viewportRect = get_viewport_rect()		# í˜„ì¬ ë·°í¬íŠ¸ í¬ê¸°ë¥¼ ì €ì¥í•©ë‹ˆë‹¤.
	
	change_scale(userConfig.avatarZoom)
	get_viewport().size_changed.connect(_on_viewport_size_changed)

# ì‚¬ìš©ì ë°ì´í„°ë¥¼ ì„¤ì •í•˜ê³  ìºë¦­í„°ë¥¼ ì´ˆê¸°í™”í•©ë‹ˆë‹¤.
func _setup(user: chatUser) -> void:
	userData = user
	nameTag.text = userData.nickname		# ì´ë¦„í‘œì— ì‚¬ìš©ì ë‹‰ë„¤ì„ì„ í‘œì‹œí•©ë‹ˆë‹¤.
	
	if userData.avatarName and userData.avatarName != "":
		var avatarDB = globalNode.avatarDatabase
		if avatarDB:
			var avatar_data = avatarDB.get_avatar_data(userData.avatarName)
			if not avatar_data.is_empty():
				change_avatar(avatar_data["PATH"])
	else:
		var avatarDB = globalNode.avatarDatabase
		if avatarDB:
			var defaultAvatar = avatarDB.get_default_avatar()
			var avatar_data = avatarDB.get_avatar_data(defaultAvatar)
			if not avatar_data.is_empty():
				change_avatar(avatar_data["PATH"])
				
	# ì‚¬ìš©ìê°€ ì„¤ì •í•œ ìƒ‰ìƒì´ ìˆìœ¼ë©´ í•´ë‹¹ ìƒ‰ìƒì„, ì—†ìœ¼ë©´ ëœë¤ ìƒ‰ìƒì„ ì ìš©í•©ë‹ˆë‹¤.
	if userData.colorName and userData.colorName != "":
		set_color_by_name(userData.colorName)
	else:
		var color_keys = COLOR_PALETTE.keys()
		var random_color = color_keys[randi() % color_keys.size()]
		set_color_by_name(random_color)


func _physics_process(delta: float) -> void:
	if isDragged:
		# ë“œë˜ê·¸ ì¤‘ì¼ ë•Œ: ë§ˆìš°ìŠ¤ ìœ„ì¹˜ë¥¼ ë”°ë¼ ì´ë™í•˜ê³ , ë˜ì§€ê¸° ì†ë„ë¥¼ ê³„ì‚°í•©ë‹ˆë‹¤.
		var mouse_pos = get_global_mouse_position()
		var target_pos = mouse_pos - dragOffset
	
		throwVelocity = (target_pos - lastPosition) / delta
		lastPosition = target_pos
		global_position = target_pos
		velocity = Vector2.ZERO
	
	else:
		if not is_on_floor():							# ê³µì¤‘ì— ë–  ìˆëŠ” ê²½ìš°, ì¤‘ë ¥ì„ ì ìš©í•©ë‹ˆë‹¤.
			velocity.y += userConfig.gravity * delta
		else:											# ì°©ì§€í•œ ê²½ìš° ìˆ˜ì§ì†ë„ë¥¼ 0ìœ¼ë¡œ ì´ˆê¸°í™”í•©ë‹ˆë‹¤.
			if velocity.y > 0: 
				velocity.y = 0
			# ë˜ì ¸ì§„ ìƒíƒœì—ì„œ ì°©ì§€í•˜ë©´ IDLE ìƒíƒœë¡œ ì „í™˜í•©ë‹ˆë‹¤.
			if currentState == State.THROWN:
				currentState = State.IDLE
				velocity.x = 0
				_reset_state_timer()
		
		if currentState != State.THROWN:				# ë˜ì ¸ì§„ ìƒíƒœê°€ ì•„ë‹ˆë©´ ìë™ ìƒíƒœ ì „í™˜ì„ ì²˜ë¦¬í•©ë‹ˆë‹¤.
			stateTransition(delta)
		
		if currentState != State.THROWN:				# ë˜ì ¸ì§„ í›„, ë˜ì§€ê¸° ì†ë„ê°€ ë‚¨ì•„ìˆìœ¼ë©´ ì„œì„œíˆ ê°ì†í•©ë‹ˆë‹¤.
			if throwVelocity.length() > 1:
				velocity = throwVelocity * 0.3
				throwVelocity = throwVelocity.lerp(Vector2.ZERO, 5.0 * delta)
			else:
				throwVelocity = Vector2.ZERO
			
			# StateMachine ìƒíƒœì— ë”°ë¥¸ ì²˜ë¦¬
			match currentState:
				State.WALK: handle_walk()
				State.SIT: velocity.x = 0		# ì•‰ì•„ ìˆê±°ë‚˜ ê°€ë§Œíˆ ìˆëŠ” ê²½ìš° ìˆ˜í‰ ì†ë„ë¥¼ 0ìœ¼ë¡œ ì„¤ì •í•©ë‹ˆë‹¤. (ë¶€ë™ìƒíƒœ)
				State.IDLE: velocity.x = 0
				
				State.JUMP, State.FALL: velocity.x = direction * userConfig.moveSpeed * 0.3
	
	spriteChange()			# í˜„ì¬ ìƒíƒœì— ë§ëŠ” ì• ë‹ˆë©”ì´ì…˜ì„ ì¬ìƒí•©ë‹ˆë‹¤.
	move_and_slide()		# Godotì˜ ë¬¼ë¦¬ ì—”ì§„ì„ ì‚¬ìš©í•˜ì—¬ ì´ë™ì„ ì²˜ë¦¬í•©ë‹ˆë‹¤.
	updateBoundary()		# í™”ë©´ ê²½ê³„ë¥¼ ë²—ì–´ë‚˜ì§€ ì•Šë„ë¡ ì²˜ë¦¬í•©ë‹ˆë‹¤.

# ì „ì—­ ì…ë ¥ ì´ë²¤íŠ¸ë¥¼ ì²˜ë¦¬í•©ë‹ˆë‹¤ (ë§ˆìš°ìŠ¤ ë²„íŠ¼ ì…ë ¥).
func _input(event: InputEvent):
	if event is InputEventMouseButton:
		# ë§ˆìš°ìŠ¤ ì™¼ìª½ ë²„íŠ¼ì„ ë†“ìœ¼ë©´ ë“œë˜ê·¸ë¥¼ ì¢…ë£Œí•©ë‹ˆë‹¤.
		if event.button_index == MOUSE_BUTTON_LEFT and not event.pressed:
			if currentState == State.GRAB:
				end_drag()
		# ë§ˆìš°ìŠ¤ ì˜¤ë¥¸ìª½ ë²„íŠ¼ì„ ëˆ„ë¥´ë©´ ì»¨í…ìŠ¤íŠ¸ ë©”ë‰´ë¥¼ í‘œì‹œí•©ë‹ˆë‹¤.
		if event.button_index == MOUSE_BUTTON_RIGHT and event.pressed:
			if mouseOver:
				show_context_menu()
				get_tree().root.set_input_as_handled()

# ìºë¦­í„° ì˜ì—­ ë‚´ ë§ˆìš°ìŠ¤ í´ë¦­ ì´ë²¤íŠ¸ë¥¼ ì²˜ë¦¬í•©ë‹ˆë‹¤.
func _input_event(viewport: Viewport, event: InputEvent, shape_idx: int):
	if event is InputEventMouseButton:
		# ìºë¦­í„°ë¥¼ í´ë¦­í•˜ë©´ ë“œë˜ê·¸ë¥¼ ì‹œì‘í•©ë‹ˆë‹¤.
		if event.button_index == MOUSE_BUTTON_LEFT and event.pressed:
			start_drag()
			get_tree().root.set_input_as_handled()

# ë¦¬ìŠ¤í° ì½œë°± í•¨ìˆ˜ë¥¼ ì„¤ì •í•©ë‹ˆë‹¤.
func _respawn_callback(callback: Callable) -> void:
	respawnCallback = callback

# â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ #

# ìºë¦­í„°ì˜ ìƒíƒœë¥¼ ìë™ìœ¼ë¡œ ì „í™˜í•©ë‹ˆë‹¤.
func stateTransition(delta: float) -> void:
	if !is_on_floor():		# ê³µì¤‘ì—ì„œ ìƒìŠ¹ ì¤‘ì´ë©´ JUMP, í•˜ê°• ì¤‘ì´ë©´ FALL ìƒíƒœë¡œ ì „í™˜í•©ë‹ˆë‹¤.
		if velocity.y < 0: currentState = State.JUMP
		else: currentState = State.FALL
		return

	if currentState == State.JUMP or currentState == State.FALL:	# ì°©ì§€ ì‹œ IDLE ìƒíƒœë¡œ ì „í™˜í•©ë‹ˆë‹¤.
		currentState = State.IDLE
		_reset_state_timer()
		return
	
	# SIT, GRAB, THROWN ìƒíƒœì—ì„œëŠ” ìë™ ì „í™˜ì„ ì°¨ë‹¨í•©ë‹ˆë‹¤.
	if currentState == State.SIT or currentState == State.GRAB or currentState == State.THROWN:
		return
	
	# ì¼ì • ì‹œê°„ì´ ì§€ë‚˜ë©´ ìƒíƒœë¥¼ ì „í™˜í•©ë‹ˆë‹¤.
	stateTimer += delta
	if stateTimer >= nextStateChange:
		match currentState:
			State.IDLE:								# IDLE -> WALK : ëœë¤ ë°©í–¥ìœ¼ë¡œ ì´ë™
				currentState = State.WALK
				direction = [-1, 1].pick_random()
			State.WALK:
				currentState = State.IDLE			# WALK -> IDLE : í•´ë‹¹ ìœ„ì¹˜ì—ì„œ ëŒ€ê¸°
		_reset_state_timer()						# ì–´ëŠ ë°©í–¥ìœ¼ë¡œë“  ì „í™˜ì´ ëë‚˜ë©´ ë‹¤ìŒ ì „í™˜ ì‹œì ì„ ê³„ì‚°í•©ë‹ˆë‹¤.

# WALK ìƒíƒœì¼ ë•Œì˜ ì´ë™ì„ ì²˜ë¦¬í•©ë‹ˆë‹¤.
func handle_walk() -> void:
	if not _is_in_viewportBound():					# í™”ë©´ ê²½ê³„ì— ë„ë‹¬í•˜ë©´ ë°©í–¥ì„ ë°˜ëŒ€ë¡œ ë°”ê¿‰ë‹ˆë‹¤.
		direction *= -1
	if is_on_wall():								# ë²½ì— ë¶€ë”ªíˆë©´ ë°©í–¥ì„ ë°˜ëŒ€ë¡œ ë°”ê¿‰ë‹ˆë‹¤.
		direction *= -1
	elif randf() < turnChance:						# ëœë¤í•˜ê²Œ ë°©í–¥ì„ ë°”ê¿‰ë‹ˆë‹¤.
		direction = [-1, 1].pick_random()
	
	velocity.x = direction * userConfig.moveSpeed

# ë“œë˜ê·¸ë¥¼ ì‹œì‘í•©ë‹ˆë‹¤.
func start_drag() -> void:
	isDragged = true
	currentState = State.GRAB
	
	dragOffset = get_global_mouse_position() - global_position
	lastPosition = global_position
	throwVelocity = Vector2.ZERO

# ë“œë˜ê·¸ë¥¼ ì¢…ë£Œí•©ë‹ˆë‹¤.
func end_drag() -> void:
	isDragged = false

	if throwVelocity.length() > 1:			# ë¹ ë¥´ê²Œ ë†“ìœ¼ë©´ ë˜ì§€ê¸° ìƒíƒœë¡œ ì „í™˜
		currentState = State.THROWN
		velocity = throwVelocity * 0.5
		throwVelocity = Vector2.ZERO
	else:									# ì²œì²œíˆ ë†“ìœ¼ë©´ ììœ ë‚™í•˜ (ì¼ë°˜ì ì¸ ë‚™í•˜)
		currentState = State.FALL
		throwVelocity = Vector2.ZERO

# ìºë¦­í„°ê°€ í™”ë©´ ì•ˆì— ìˆëŠ”ì§€ í™•ì¸í•©ë‹ˆë‹¤.
func _is_in_viewportBound() -> bool:
	var margin = 20.0
	return (global_position.x > margin and 
			global_position.x < viewportRect.size.x - margin)

# â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ #

# ì±„íŒ… ë§í’ì„ ì„ í‘œì‹œí•©ë‹ˆë‹¤.
func show_chatbubble(CHAT: String):
	# ëŠë‚Œí‘œ(!)ë¡œ ì‹œì‘í•˜ëŠ” ë©”ì‹œì§€ëŠ” ëª…ë ¹ì–´ë¡œ ì²˜ë¦¬í•©ë‹ˆë‹¤.
	if CHAT.to_lower().begins_with(">"):
		handle_command(CHAT)
	
	elif CHAT.to_lower().begins_with("ğŸ¥«"):
		return
		
	else:
		# ë§í’ì„  ì¸ìŠ¤í„´ìŠ¤ë¥¼ ìƒì„±í•˜ê³  í™”ë©´ì— ì¶”ê°€í•©ë‹ˆë‹¤.
		var bubble = chatBubble.instantiate()
		$ChatContainer.add_child(bubble)
		bubble.get_node("MarginContainer/PanelContainer").set_text(CHAT)
		
		# ë§í’ì„ ì„ í˜ì´ë“œì¸ íš¨ê³¼ë¡œ í‘œì‹œí•©ë‹ˆë‹¤.
		bubble.modulate.a = 0.0
		var tween = create_tween()
		tween.tween_property(bubble, "modulate:a", 1.0, 0.2)
		
		# ìµœëŒ€ ê°œìˆ˜ë¥¼ ì´ˆê³¼í•˜ë©´ ê°€ì¥ ì˜¤ë˜ëœ ë§í’ì„ ì„ ì œê±°í•©ë‹ˆë‹¤.
		if $ChatContainer.get_child_count() > userConfig.maxChatStack:
			var oldest = $ChatContainer.get_child(0)
			remove_chatBubble(oldest)
		
		# ë§í’ì„  ì§€ì† ì‹œê°„ì´ ì´ˆê³¼ëœ ê²½ìš° ë§í’ì„ ì„ ì œê±°í•©ë‹ˆë‹¤.
		await get_tree().create_timer(userConfig.bubbleDuration).timeout
		if is_instance_valid(bubble):
			remove_chatBubble(bubble)

# ë§í’ì„ ì„ í˜ì´ë“œì•„ì›ƒ íš¨ê³¼ë¡œ ì œê±°í•©ë‹ˆë‹¤.
func remove_chatBubble(bubble: Node):
	if not is_instance_valid(bubble): return
	
	var tween = create_tween()
	tween.tween_property(bubble, "modulate:a", 0.0, 0.3)
	tween.finished.connect(func(): 
		if is_instance_valid(bubble):
			bubble.queue_free()
		tween.kill())

# ì±„íŒ… ëª…ë ¹ì–´ë¥¼ ì²˜ë¦¬í•©ë‹ˆë‹¤.
func handle_command(CMD: String):
	var parts = CMD.to_lower().strip_edges().split(" ")
	var command = parts[0]
	
	match command:
		">jump", ">ì í”„":			# ìºë¦­í„° ì í”„ - ì•‰ì•„ìˆê±°ë‚˜ ê³µì¤‘ì— ìˆëŠ” ê²½ìš° ì ìš©ë˜ì§€ ì•ŠìŒ
			if currentState == State.SIT: return
			if is_on_floor():
				velocity.y = -userConfig.jumpSpeed
				currentState = State.JUMP
		">sit", ">ì•‰ì•„":				# ìºë¦­í„° ì•‰íˆê¸° - ì§€ë©´ì— ìˆëŠ” ê²½ìš°ì—ë§Œ ì‹¤í–‰ë˜ê³ , ëŒ€ë¶€ë¶„ì˜ ìƒíƒœì „í™˜ì„ ë¬´ì‹œí•©ë‹ˆë‹¤.
			if is_on_floor(): currentState = State.SIT
		">stand", ">ì¼ì–´ë‚˜":			# ìºë¦­í„° ì¼ìœ¼í‚¤ê¸° - ì•‰ì€ ìƒíƒœì—ì„œ ì¼ì–´ë‚˜ëŠ” ì• ë‹ˆë©”ì´ì…˜ì„ ì¬ìƒí•©ë‹ˆë‹¤.
			if currentState == State.SIT and is_on_floor():
				animationOverride = true	# ë‹¤ë¥¸ ì• ë‹ˆë©”ì´ì…˜ì´ ì¬ìƒë˜ì§€ ì•Šë„ë¡ ì ê¸ˆ
				$sprite.play("stand")
				await $sprite.animation_finished
				currentState = State.IDLE
				animationOverride = false 
				_reset_state_timer()
		">color", ">ìƒ‰ë°”ê¾¸ê¸°":		# ìƒ‰ìƒ ë°”ê¾¸ê¸° - ìƒ‰ìƒ ì´ë¦„ì´ ì£¼ì–´ì§€ë©´ í•´ë‹¹ ìƒ‰ìƒìœ¼ë¡œ ë³€ê²½í•©ë‹ˆë‹¤.
			if parts.size() > 1:
				set_color_by_name(parts[1])
			else:
				var colorNames = COLOR_PALETTE.keys()
				globalNode.mainNode.chatMessage("ğŸ¥« ì‚¬ìš©í•  ìˆ˜ ìˆëŠ” ìƒ‰ìƒ: %s" % str(colorNames))
		">help", ">ë„ì›€ë§":
			globalNode.mainNode.chatMessage("ğŸ¥« ì‚¬ìš©í•  ìˆ˜ ìˆëŠ” ëª…ë ¹ì–´: ì í”„, ì•‰ì•„, ì¼ì–´ë‚˜, ìƒ‰ë°”ê¾¸ê¸°, ë„ì›€ë§")
		_:
			globalNode.mainNode.chatMessage("ğŸ¥« ì•Œ ìˆ˜ ì—†ëŠ” ëª…ë ¹ì–´ì…ë‹ˆë‹¤.")

# â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ #

# í˜„ì¬ ìƒíƒœì— ë§ëŠ” ìŠ¤í”„ë¼ì´íŠ¸ ì• ë‹ˆë©”ì´ì…˜ì„ ì¬ìƒí•©ë‹ˆë‹¤.
func spriteChange():
	if direction < 1: $sprite.flip_h = true			# ì´ë™ ë°©í–¥ì— ë§ê²Œ ìŠ¤í”„ë¼ì´íŠ¸ë¥¼ ì¢Œìš°ë°˜ì „í•©ë‹ˆë‹¤.
	elif direction > 0: $sprite.flip_h = false		# -1(ì™¼ìª½)ì¸ ê²½ìš° ì¢Œìš°ë°˜ì „, 1(ì˜¤ë¥¸ìª½)ì¸ ê²½ìš° ê·¸ëŒ€ë¡œ
	
	if animationOverride:							# ì• ë‹ˆë©”ì´ì…˜ ì˜¤ë²„ë¼ì´ë“œê°€ í™œì„±í™”ë˜ì–´ ìˆìœ¼ë©´ ìƒíƒœ ë³€ê²½ì„ ë¬´ì‹œí•©ë‹ˆë‹¤.
		return
	else:
		match currentState:
			State.IDLE: $sprite.play("idle")
			State.WALK: $sprite.play("walk")
			State.JUMP: $sprite.play("jump")
			State.FALL: $sprite.play("fall")
			State.SIT: 
				if $sprite.animation != "sit":		# ì´ë¯¸ ì•‰ê¸° ì• ë‹ˆë©”ì´ì…˜ì´ ì¬ìƒ ì¤‘ì´ ì•„ë‹ ë•Œë§Œ ì¬ìƒí•©ë‹ˆë‹¤. (ë°˜ë³µì¬ìƒ ë°©ì§€)
					$sprite.play("sit")
			State.THROWN: $sprite.play("fall")
			State.GRAB: $sprite.play("grab")
			State.DANCE: pass

# ìƒ‰ìƒ ì´ë¦„ìœ¼ë¡œ ìºë¦­í„°ì˜ ìƒ‰ìƒì„ ë³€ê²½í•©ë‹ˆë‹¤.
func set_color_by_name(COLOR: String):
	if COLOR_PALETTE.has(COLOR):
		var hue = COLOR_PALETTE[COLOR]
		sprite_hue_shift(hue)
		userData.hueShift = hue
		userData.colorName = COLOR
		
		if userDatabase and userData.channelId:
			userDatabase.update_user_color(userData.channelId, hue, COLOR)
	else:
		pass

# ìŠ¤í”„ë¼ì´íŠ¸ì˜ ìƒ‰ì¡°(Hue)ë¥¼ ë³€ê²½í•©ë‹ˆë‹¤.
func sprite_hue_shift(HUE: float):
	if sprite.material:
		sprite.material.set_shader_parameter("hue", HUE)
	
	if userData:
		userData.hueShift = HUE		# ìƒ‰ìƒ ë³€ê²½ í›„ ì‚¬ìš©ì ì •ë³´ì— Hue Shift ê°’ì„ ì €ì¥í•©ë‹ˆë‹¤.


func change_avatar(avatar_path: String) -> void:
	if ResourceLoader.exists(avatar_path):
		var avatar_scene = load(avatar_path)

		sprite.sprite_frames = avatar_scene
		change_scale(userConfig.avatarZoom)

		print("[Character] ì•„ë°”íƒ€ ë³€ê²½ë¨: %s" % avatar_path)
	else:
		push_error("[Character] ì•„ë°”íƒ€ íŒŒì¼ì„ ì°¾ì„ ìˆ˜ ì—†ìŒ: %s" % avatar_path)
		sprite.sprite_frames = defaultSpriteSet
		change_scale(userConfig.avatarZoom)
		
func change_scale(scale_value: float) -> void:
	var scaleTarget = Vector2(scale_value, scale_value)
	
	var texture = sprite.sprite_frames.get_frame_texture("idle", 0)
	var texSize = texture.get_size() 
	var nametagOffset = -(texSize.y * 2.0 * scaleTarget.y / 2) - 16
	var chatOffset = nametagOffset - 28

	$Collision.shape.size = texSize * 2.0 * scaleTarget.x
	$Nametag.position.y = nametagOffset
	$ChatContainer.position.y = chatOffset
	$sprite.scale = 2.0 * scaleTarget

# â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ #

# í™”ë©´ ê²½ê³„ë¥¼ ë²—ì–´ë‚˜ì§€ ì•Šë„ë¡ ìœ„ì¹˜ë¥¼ ì œí•œí•©ë‹ˆë‹¤.
func updateBoundary() -> void:
	
	# ì¼ë°˜ ìƒíƒœì¼ ë•ŒëŠ” í™”ë©´ ì•ˆì— ìœ„ì¹˜í•˜ë„ë¡ ì œí•œí•©ë‹ˆë‹¤.
	if currentState != State.THROWN and not isDragged:
		if global_position.x < 0:
			global_position.x = 0
			velocity.x = 0
		elif global_position.x > viewportRect.size.x:
			global_position.x = viewportRect.size.x
			velocity.x = 0
			
		if global_position.y < 0:
			global_position.y = 0
			velocity.y = 0

	# ë˜ì ¸ì§„ ìƒíƒœì¼ ë•ŒëŠ” ì™„ì „íˆ í™”ë©´ ë°–ìœ¼ë¡œ ë‚˜ê°€ë©´ ë¦¬ìŠ¤í°ì„ ìš”ì²­í•©ë‹ˆë‹¤.
	elif currentState == State.THROWN:
		var oobDetection = (
			global_position.x < -boundaryMargin or
			global_position.x > viewportRect.size.x + boundaryMargin or
			global_position.y < -boundaryMargin or
			global_position.y > viewportRect.size.y + boundaryMargin
		)
		
		if oobDetection: _request_respawn()

# ìºë¦­í„° ìš°í´ë¦­ ì‹œ ì»¨í…ìŠ¤íŠ¸ ë©”ë‰´ë¥¼ í‘œì‹œí•©ë‹ˆë‹¤.
func show_context_menu():
	var context_menu = get_tree().root.get_node("/root/mainWindow/UI/ContextMenu")
	if context_menu: context_menu.show_character_menu(self)

# â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ #

# ìƒíƒœ íƒ€ì´ë¨¸ë¥¼ ì´ˆê¸°í™”í•˜ê³  ë‹¤ìŒ ìƒíƒœ ë³€ê²½ ì‹œê°„ì„ ëœë¤í•˜ê²Œ ì„¤ì •í•©ë‹ˆë‹¤.
func _reset_state_timer():
	stateTimer = 0.0
	nextStateChange = randf_range(userConfig.stateMin, userConfig.stateMax)

# ìºë¦­í„°ê°€ ì°½ ë°–ìœ¼ë¡œ ë„˜ì–´ê°„ ê²½ìš°, ë¦¬ìŠ¤í°ì„ ìš”ì²­í•©ë‹ˆë‹¤.
func _request_respawn():
	if respawnCallback:
		respawnCallback.call(self)
	else:
		queue_free()

func _on_viewport_size_changed() -> void:
	viewportRect = get_viewport_rect()
	
	# ì°½ í¬ê¸°ê°€ ì‘ì•„ì ¸ì„œ ìºë¦­í„°ê°€ í™”ë©´ ë°–ì— ìˆìœ¼ë©´ ì•ˆìª½ìœ¼ë¡œ ì´ë™ì‹œí‚µë‹ˆë‹¤.
	if global_position.x > viewportRect.size.x:
		global_position.x = viewportRect.size.x - 10
	if global_position.y > viewportRect.size.y:
		global_position.y = viewportRect.size.y - 10

func _on_mouse_entered() -> void:
	mouseOver = true

func _on_mouse_exited() -> void:
	mouseOver = false
